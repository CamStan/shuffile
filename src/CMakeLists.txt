# Header files need not be enumerated
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR} ${PROJECT_BINARY_DIR})

###########
# LIB ER #
###########

# Install header files
LIST(APPEND libshuffile_install_headers
    shuffile.h
)
INSTALL(FILES ${libshuffile_install_headers} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

LIST(APPEND libshuffile_srcs
    shuffile_util.c
    shuffile_io.c
    shuffile.c
)

IF(BUILD_SHARED_LIBS)
   # Shuffile Library
   ADD_LIBRARY(shuffile_o OBJECT ${libshuffile_srcs})
   TARGET_LINK_LIBRARIES(shuffile_o PRIVATE ${SHUFFILE_EXTERNAL_LIBS})

   ADD_LIBRARY(shuffile SHARED $<TARGET_OBJECTS:shuffile_o>)
   ADD_LIBRARY(shuffile::shuffile ALIAS shuffile)
   TARGET_LINK_LIBRARIES(shuffile PUBLIC ${SHUFFILE_EXTERNAL_LIBS})
   TARGET_INCLUDE_DIRECTORIES(shuffile PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}> $<INSTALL_INTERFACE:include>)
   SET_TARGET_PROPERTIES(shuffile PROPERTIES OUTPUT_NAME shuffile CLEAN_DIRECT_OUTPUT 1)
   INSTALL(TARGETS shuffile EXPORT shuffileTargets LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR})
ENDIF()

# Shuffile Library
ADD_LIBRARY(shuffile_static_o OBJECT ${libshuffile_srcs})
TARGET_LINK_LIBRARIES(shuffile_static_o PRIVATE ${SHUFFILE_EXTERNAL_STATIC_LIBS})

ADD_LIBRARY(shuffile-static STATIC $<TARGET_OBJECTS:shuffile_static_o>)
ADD_LIBRARY(shuffile::shuffile-static ALIAS shuffile-static)
IF(KVTREE_LINK_STATIC)
        SET_TARGET_PROPERTIES(shuffile-static PROPERTIES LINK_SEARCH_START_STATIC 1)
        SET_TARGET_PROPERTIES(shuffile-static PROPERTIES LINK_SEARCH_END_STATIC 1)
ENDIF(KVTREE_LINK_STATIC)
TARGET_LINK_LIBRARIES(shuffile-static PUBLIC ${SHUFFILE_EXTERNAL_STATIC_LIBS})
TARGET_INCLUDE_DIRECTORIES(shuffile-static PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}> $<INSTALL_INTERFACE:include>)

SET_TARGET_PROPERTIES(shuffile-static PROPERTIES OUTPUT_NAME shuffile CLEAN_DIRECT_OUTPUT 1)
INSTALL(TARGETS shuffile-static EXPORT shuffileTargets LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR})
